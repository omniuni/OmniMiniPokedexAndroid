plugins {
	alias(libs.plugins.androidApplication)
	alias(libs.plugins.jetbrainsKotlinAndroid)
	id('base')
	id("com.google.devtools.ksp").version("2.0.0-1.0.22")
}

kotlin {
	jvmToolchain 17
}

// Load keystore properties for debug builds
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystorePropertiesProdFile = rootProject.file("keystore-prod.properties")
def keystoreProperties = new Properties()
def keystorePropertiesProd = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
keystorePropertiesProd.load(new FileInputStream(keystorePropertiesProdFile))

android {
	namespace 'com.omniimpact.mini.pokedex'
	compileSdk 34

	signingConfigs {
		config {
			storeFile file(keystoreProperties['storeFile'])
			storePassword keystoreProperties['storePassword']
			keyAlias keystoreProperties['keyAlias']
			keyPassword keystoreProperties['keyPassword']
		}
		configProd {
			storeFile file(keystorePropertiesProd['storeFile'])
			storePassword keystorePropertiesProd['storePassword']
			keyAlias keystorePropertiesProd['keyAlias']
			keyPassword keystorePropertiesProd['keyPassword']
		}
	}

	def customArchiveName
	defaultConfig {
		applicationId "com.omniimpact.mini.pokedex"
		minSdk 27
		targetSdk 34
		// https://semver.org/
		versionName androidGitVersion.name()
		versionCode androidGitVersion.code()
		customArchiveName = "$applicationId-$versionCode-$versionName"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		vectorDrawables {
			useSupportLibrary true
		}

		buildConfigField("String", "APP_VERSION_NAME", "\"$versionName\"")
		buildConfigField("int", "APP_VERSION_CODE", "$versionCode")

		signingConfig signingConfigs.config
	}

	base {
		archivesName = customArchiveName
	}

	flavorDimensions += "devTools"
	productFlavors {
		devTools {
			dimension "devTools"
		}
		app {
			dimension "devTools"
		}
	}
	buildTypes {
		debug {
			signingConfig signingConfigs.config
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			testProguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			applicationIdSuffix ".debug"
		}
		release {
			signingConfig signingConfigs.configProd
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			testProguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			applicationIdSuffix ".app"
		}
	}
	buildFeatures {
		viewBinding true
		buildConfig true
	}

}

dependencies {
	// Google Libraries
	implementation libs.androidx.core.ktx
	implementation libs.androidx.lifecycle.runtime.ktx
	implementation libs.material

	// Supporting Libraries
	// https://github.com/square/moshi
	ksp(libs.moshi.kotlin.codegen)
	implementation libs.moshi.kotlin

	// Picasso
	implementation libs.picasso

	// Palette
	implementation libs.palette

	// Testing Libraries
	testImplementation libs.junit
	androidTestImplementation libs.androidx.junit
	androidTestImplementation libs.androidx.espresso.core
}
